 
    model { 
    # Priors
    phi~dunif(0,1)
    p0~dunif(0,1)
    lambda~dunif(0,100)
    
    # Likelihood 
    
    # Biological model for true abundance 
    for (i in 1:S) { 
      M[i] ~ dpois(lambda)
      
      # Observation model for replicated counts 
      for (j in 1:J) { 
        avails[i,j] ~ dbin(phi , M[i])
        
        # Define multinomial cell probabilities
        p[i,j] <- p0
        mu[i,1,j] <- p[i,j] * p[i,j]         # 11
        mu[i,2,j] <- p[i,j] * (1-p[i,j])     # 10     
        mu[i,3,j] <- (1-p[i,j]) * p[i,j]     # 01
        pi0[i,j] <- 1 - mu[i,1,j] - mu[i,2,j] - mu[i,3,j]
        pcap[i,j] <- 1 - pi0[i,j] #Probability of sighting by at least one observer
        for (k in 1:3) {
          muc[i,k,j] <- mu[i,k,j] / pcap[i,j]
        } #k
      
        # Observation model for multiple observers - conditional multinomial
        ncap[i,j] ~ dbin(pcap[i,j] , avails[i,j])
        Y[i,1:3,j] ~ dmulti(muc[i,1:3,j] , ncap[i,j])
      } #j
    } #i
    
    # Derived quantities
    totalM <- sum(M[])
    
    } #model
    
